; MTBindingSim NSIS Installer Script (full installer + prereqs)
;
; This file is part of MTBindingSim.
;
; Copyright (C) 2010-2013  University of Notre Dame
;
; MTBindingSim is free software: you can redistribute it and/or modify
; it under the terms of the GNU General Public License as published by
; the free Software Foundation, either version 3 of the License, or
; (at your option) any later version.
;
; MTBindingSim is distributed in the hope that it will be useful,
; but WITHOUT ANY WARRANTY; without even the implied warranty of
; MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
; GNU General Public License for more details.
;
; You should have received a copy of the GNU General Public License
; along with MTBindingSim.  If not, see <http://www.gnu.org/licenses/>.

!include "MUI2.nsh"
!include "WinVer.nsh"


;; Name and output
Name "MTBindingSim"
OutFile "@top_builddir@\MTBindingSim-@PACKAGE_VERSION@.exe"
SetCompressor /SOLID lzma

;; Default installation folder
InstallDir "$PROGRAMFILES\MTBindingSim"

;; Get the installation directory from the registry, if possible
InstallDirRegKey HKCU "Software\MTBindingSim" "Install Directory"

;; Request admin rights on Vista and higher
RequestExecutionLevel admin


;; Interface settings
Var StartMenuFolder
!define MUI_ABORTWARNING


;; Pages
!insertmacro MUI_PAGE_WELCOME
!insertmacro MUI_PAGE_LICENSE "..\COPYING"
!insertmacro MUI_PAGE_DIRECTORY

!define MUI_STARTMENUPAGE_REGISTRY_ROOT "HKCU"
!define MUI_STARTMENUPAGE_REGISTRY_KEY "Software\MTBindingSim"
!define MUI_STARTMENUPAGE_REGISTRY_VALUENAME "Start Menu Folder"

!insertmacro MUI_PAGE_STARTMENU Application $StartMenuFolder

!insertmacro MUI_PAGE_INSTFILES

!define MUI_FINISHPAGE_RUN "$INSTDIR/MTBindingSim.exe"
!define MUI_FINISHPAGE_RUN_NOTCHECKED
!define MUI_FINISHPAGE_SHOWREADME http://bindingtutor.org/mtbindingsim/
!define MUI_FINISHPAGE_SHOWREADME_TEXT "Visit the MTBindingSim home page"
!define MUI_FINISHPAGE_SHOWREADME_NOTCHECKED

!insertmacro MUI_PAGE_FINISH

!insertmacro MUI_UNPAGE_WELCOME
!insertmacro MUI_UNPAGE_CONFIRM
!insertmacro MUI_UNPAGE_INSTFILES
!insertmacro MUI_UNPAGE_FINISH


;; Languages
!insertmacro MUI_LANGUAGE "English"


;; Installer sections
Var WindowsPlatform
Var VCTest

;; Get the Windows platform (x86, ia64, x64)
Function GetWindowsPlatform
  StrCpy $WindowsPlatform "0"
  
  ${If} ${AtLeastWinXP}
    System::Alloc 36
    pop $0
    ${If} $0 <> 0
      System::Call 'kernel32::GetNativeSystemInfo(i $0)'
      System::Call "*$0(&i2.r1)"
      StrCpy $WindowsPlatform $1
      System::Free $0
    ${EndIf}
  ${EndIf}
FunctionEnd


Section -Prerequisites
  
  SetOutPath $INSTDIR\redist
  Call GetWindowsPlatform

  File "redist\vcredist_x86.exe"
  ExecWait '"$INSTDIR\redist\vcredist_x86.exe" /q /norestart' $VCTest
  
  ${If} $VCTest == "3010"
    SetRebootFlag true
  ${ElseIf} $VCTest != "0"
    Abort "Failed to install the Visual C++ 2010 redistributable package"
  ${EndIf}
  
  IfFileExists "$PROGRAMFILES\MATLAB\MATLAB Compiler Runtime\v80\license.txt" MCRInstalled
  
  ;; Get the MCR from the ND website
  InitPluginsDir
  Push $0
  ClearErrors
  
  inetc::get /RESUME "Your internet connection seems to have dropped out!\nPlease reconnect and click Retry to resume downloading..." /CAPTION "MTBindingSim" /POPUP "Downloading MATLAB Compiler Runtime" "http://www.mathworks.com/supportfiles/MCR_Runtime/R2012b/MCR_R2012b_win32_installer.exe" "$PLUGINSDIR\mcr.exe" /END
  Pop $0
  
  ${Unless} $0 == "OK"
    SetErrors
    Abort "Failed to download the MATLAB Compiler Runtime: $0"
  ${EndUnless}
  
  Pop $0
  
  ;; Unzip the MCR
  CreateDirectory "$PLUGINSDIR\mcr"
  nsisunz::Unzip "$PLUGINSDIR\mcr.exe" "$PLUGINSDIR\mcr"
  Pop $0
  ${Unless} $0 == "success"
    Abort "Failed to unpack the MATLAB Compiler Runtime: $0"
  ${EndUnless}
  
  ;; Call the MCR installer
  MessageBox MB_OK "Press OK to launch the MATLAB Compiler Runtime installer.  Wait for it to start, and allow it to complete.  When it is finished, return to the MTBindingSim installer."
  
  SetOutPath $PLUGINSDIR\mcr
  ExecWait '"$PLUGINSDIR\mcr\setup.exe" -mode automated'
  
  MessageBox MB_OK "Press OK when the MATLAB Compiler Runtime installer has finished."
  
  MCRInstalled:
  
SectionEnd


Section "MTBindingSim" SecMTBS

  SetOutPath "$INSTDIR"
  
  File "@top_builddir@\src\MTBindingSim.exe"
  File "@top_srcdir@\AUTHORS"
  File "@top_srcdir@\COPYING"
  File "@top_srcdir@\ChangeLog"
  File "@top_srcdir@\ChangeLog-2010"
  File "@top_srcdir@\NEWS"
  File "@top_srcdir@\README"
  File "@top_srcdir@\THANKS"
  File "@top_builddir@\doc\mtbindingsim.pdf"
  
  ;; Store the installation directory
  WriteRegStr HKCU "Software\MTBindingSim" "Install Directory" $INSTDIR
  
  ;; Create uninstaller
  WriteUninstaller "$INSTDIR\Uninstall.exe"
  
  !insertmacro MUI_STARTMENU_WRITE_BEGIN Application
    CreateDirectory "$SMPROGRAMS\$StartMenuFolder"
    CreateShortCut "$SMPROGRAMS\$StartMenuFolder\MTBindingSim.lnk" "$INSTDIR\MTBindingSim.exe"
    CreateShortCut "$SMPROGRAMS\$StartMenuFolder\User Manual.lnk" "$INSTDIR\mtbindingsim.pdf"
    CreateShortCut "$SMPROGRAMS\$StartMenuFolder\Uninstall.lnk" "$INSTDIR\Uninstall.exe"
  !insertmacro MUI_STARTMENU_WRITE_END

SectionEnd


;; Uninstaller section
Section "Uninstall"
  
  Delete "$INSTDIR\MTBindingSim.exe"
  Delete "$INSTDIR\AUTHORS"
  Delete "$INSTDIR\COPYING"
  Delete "$INSTDIR\ChangeLog"
  Delete "$INSTDIR\ChangeLog-2010"
  Delete "$INSTDIR\NEWS"
  Delete "$INSTDIR\README"
  Delete "$INSTDIR\THANKS"
  Delete "$INSTDIR\mtbindingsim.pdf"
  Delete "$INSTDIR\Uninstall.exe"
  
  Delete "$INSTDIR\redist\vcredist_x86.exe"
  RMDir "$INSTDIR\redist"
  
  RMDir "$INSTDIR"
  
  !insertmacro MUI_STARTMENU_GETFOLDER Application $StartMenuFolder
  Delete "$SMPROGRAMS\$StartMenuFolder\MTBindingSim.lnk"
  Delete "$SMPROGRAMS\$StartMenuFolder\User Manual.lnk"
  Delete "$SMPROGRAMS\$StartMenuFolder\Uninstall.lnk"
  RMDir "$SMPROGRAMS\$StartMenuFolder"
  
  DeleteRegKey HKCU "Software\MTBindingSim"

SectionEnd
